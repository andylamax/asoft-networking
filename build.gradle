buildscript {
    ext {
        kotlin_version = "1.3.61"
        ktor_version = "1.3.0"
        io_version = "3.0.0"
        android_tools_version = "3.5.0"
    }
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:$android_tools_version")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.61'
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url "https://jitpack.io" }
}

group 'tz.co.asoft'
version '1.0.0'

apply plugin: "com.android.library"
apply plugin: "maven-publish"

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            java.srcDirs += 'src/androidMain/kotlin'
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
        publishLibraryVariants("release")
    }

    js() {
        browser()
        nodejs()
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = 'commonjs'
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api kotlin('stdlib-common')
                api("io.ktor:ktor-client-core:$ktor_version")
                api("com.github.andylamax.asoft-io:asoft-io-metadata:$io_version")
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }

        androidMain {
            dependencies {
                api kotlin('stdlib')
                api "io.ktor:ktor-client-android:$ktor_version"
                api("com.github.andylamax.asoft-io:asoft-io-android:$io_version")
            }
        }

        jvmMain {
            dependencies {
                api kotlin('stdlib')
                api "io.ktor:ktor-client-cio:$ktor_version"
                api("com.github.andylamax.asoft-io:asoft-io-jvm:$io_version")
            }
        }

        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }

        jsMain {
            dependencies {
                api kotlin('stdlib-js')
                api "io.ktor:ktor-client-js:$ktor_version"
                api(npm("text-encoding", "0.7.0"))
                api("com.github.andylamax.asoft-io:asoft-io-js:$io_version")
            }
        }

        jsTest {
            dependsOn commonTest
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}